FUNCTION_BLOCK turistear

// Define input variables
VAR_INPUT				
	presupuesto : REAL;
	clima: REAL;
    	dias_de_estadia : REAL;
	costo_vida : REAL;
END_VAR

// Define output variable
VAR_OUTPUT				
	hotel : REAL;
	lugar: REAL;
END_VAR


// Fuzzify input variable 'presupuesto': {'bajo', 'medio' , 'alto'}
FUZZIFY presupuesto			
	TERM bajo := (2000, 1) (4000, 1) (6000, 0) ; 
	TERM medio := (4500, 0) (9500, 1) (11500, 1) (15000,0);
	TERM alto := (12400, 0) (30000, 1) (30000, 1);
END_FUZZIFY

// Fuzzify input variable 'clima': {'frio', 'tropical’ , 'caluroso'}
FUZZIFY clima		
	TERM frio := (0, 1) (8, 1) (18, 0) ; 
	TERM tropical:= (15, 0) (20, 1) (32, 0);
	TERM caluroso := (29, 0) (35, 1) (50, 1);
END_FUZZIFY

// Fuzzify input variable 'dias_de_estadia': {'pocos', 'algunos' , 'muchos'}
FUZZIFY dias_de_estadia		
	TERM pocos := (0,1) (3,1) (8, 0) ; 
	TERM algunos := (4, 0) (13, 1) (20,0);
	TERM muchos := (17, 0) (22, 1) (30, 1);
END_FUZZIFY

// Fuzzify input variable 'costo_vida': {'bajo', 'medio' , 'alto'}
FUZZIFY costo_vida			
	TERM bajo := (0, 1) (49, 1) (90, 0) ; 
	TERM medio := (68.2, 0) (99, 1) (170,0);
	TERM alto := (148, 0) (279, 1) (279, 1);
END_FUZZIFY


// Defuzzify output variable 'hotel’' : {‘D’', 'E', 'B', 'A', 'AA'}
DEFUZZIFY hotel			
	TERM D := (0, 1) (1, 1) (1.5, 0);
	TERM E := (1, 0) (2, 1) (2.5, 0);
	TERM B := (2, 0) (3, 1) (3.5, 0);
	TERM A := (3, 0) (4, 1) (4.5, 0);
	TERM AA := (4, 0) (5, 1);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

// Defzzzify output variable 'lugar' : {'tipo1', 'tipo2', 'tipo3', 'tipo4', 'tipo5', 'tipo6', 'tipo7', 'tipo8', 'tipo9'}
DEFUZZIFY lugar			
	TERM tipo1 := (0, 1) (1.5,0);
	TERM tipo2 := (0.5, 0) (1,1) (2, 0);
	TERM tipo3 := (1.5, 0) (2,1) (3, 0);
	TERM tipo4 := (2.5, 0) (3,1) (4, 0);
	TERM tipo5 := (3.5, 0) (4,1) (5, 0);
	TERM tipo6 := (4.5, 0) (5,1) (6, 0);
	TERM tipo7 := (5.5, 0) (6,1) (7, 0);
	TERM tipo8 := (6.5, 0) (7,1) (8, 0);
	TERM tipo9 := (7.5, 0) (8,1) (9, 0);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY
	
// Inference rules
RULEBLOCK No1
	AND : MIN;	// Use 'min' for 'and'
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method

RULE 1: IF clima IS frio AND costo_vida IS bajo THEN lugar IS tipo1;
RULE 2: IF clima IS frio AND costo_vida IS medio THEN lugar IS tipo2 ;
RULE 3: IF clima IS frio AND costo_vida IS alto THEN  lugar IS tipo3 ;
RULE 4: IF clima IS tropical AND costo_vida IS bajo THEN lugar IS tipo4 ;
RULE 5: IF clima IS tropical AND costo_vida IS medio THEN lugar IS tipo5 ;
RULE 6: IF clima IS tropical AND costo_vida IS alto THEN lugar IS tipo6 ;
RULE 7: IF clima IS caluroso AND costo_vida IS bajo THEN lugar IS tipo7 ;
RULE 8: IF clima IS caluroso AND costo_vida IS medio THEN lugar IS tipo8 ;
RULE 9: IF clima IS caluroso AND costo_vida IS alto THEN lugar IS tipo9 ;
RULE 10: IF presupuesto IS bajo AND dias_de_estadia IS muchos THEN hotel IS D ;
RULE 11: IF presupuesto IS alto AND dias_de_estadia IS muchos THEN hotel IS A ;
RULE 12: IF presupuesto IS alto AND dias_de_estadia IS algunos THEN hotel IS AA ;
RULE 13: IF presupuesto IS medio AND dias_de_estadia IS algunos THEN hotel IS B ;
RULE 14: IF presupuesto IS medio AND dias_de_estadia IS muchos THEN hotel IS E;
RULE 15: IF presupuesto IS medio AND dias_de_estadia IS pocos THEN hotel IS A ;
RULE 16: IF presupuesto IS bajo AND dias_de_estadia IS algunos AND costo_vida IS bajo THEN hotel IS E; 
RULE 17: IF presupuesto IS bajo AND costo_vida IS alto THEN hotel IS D ;
RULE 18: IF presupuesto IS bajo AND dias_de_estadia IS pocos THEN hotel IS B;
RULE 19: IF presupuesto IS alto AND costo_vida IS bajo THEN hotel IS AA;
RULE 20: IF presupuesto IS alto AND costo_vida IS medio THEN hotel IS A;
RULE 21: IF presupuesto IS alto AND costo_vida IS alto THEN hotel IS A;
RULE 22: IF presupuesto IS medio AND dias_de_estadia IS muchos AND costo_vida IS alto THEN hotel IS D ;
RULE 23: IF presupuesto IS alto AND dias_de_estadia IS pocos THEN hotel IS AA; 

END_RULEBLOCK

END_FUNCTION_BLOCK